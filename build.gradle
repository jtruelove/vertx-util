apply plugin: 'java'
// Uncomment below when doing official builds
// apply from: "gradle/sona.gradle"

version = '0.7.0'
group = "com.jtruelove.vertx"
archivesBaseName = "vertx-util"

if (!JavaVersion.current().java8Compatible) {
    throw new IllegalStateException('''A Haiku:
                                      |  This needs Java 8,
                                      |  You are using something else,
                                      |  Refresh. Try again.'''.stripMargin())
}

repositories {
    mavenCentral()
    maven {
        url = 'http://oss.sonatype.org/content/repositories/snapshots/'
    }
}

dependencies {
    compile 'io.vertx:vertx-core:3.4.2'
    compile 'io.vertx:vertx-web:3.4.2'
    compile "io.vertx:vertx-codegen:3.4.2"
    compile 'org.slf4j:slf4j-api:1.7.7'
    compile 'commons-lang:commons-lang:2.6'
    compile 'javax.ws.rs:javax.ws.rs-api:2.0.1'
    compile "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.6.1"
    testCompile "junit:junit:4.11"
    testCompile "io.vertx:vertx-unit:3.4.2"
    testCompile 'io.vertx:vertx-web:3.4.2:tests'
    testCompile 'io.vertx:vertx-core:3.4.2:tests'
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.1'
}

task release() << {}

gradle.taskGraph.whenReady {taskGraph ->
    if (!taskGraph.hasTask(release)) {
        version += '-SNAPSHOT'
    }
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}
